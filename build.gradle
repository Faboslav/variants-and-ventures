plugins {
	id "architectury-plugin" version "3.4-SNAPSHOT"
	id 'dev.architectury.loom' version '1.6-SNAPSHOT' apply false
	id "io.github.pacifistmc.forgix" version "1.2.9"
}

architectury {
	minecraft = rootProject.minecraft_version
}

forgix {
	group = "com.faboslav.variantsandventures"
	mergedJarName = "${rootProject.mod_id}-mc${rootProject.minecraft_version}-${rootProject.mod_version}.jar"
	outputDir = "build/libs/merged"

	if (findProject(":fabric"))
		fabric {
			projectName = "fabric"
			jarLocation = "build/libs/${rootProject.mod_id}-fabric-mc${rootProject.minecraft_version}-${rootProject.mod_version}.jar"
		}

	if (findProject(":forge"))
		forge {
			projectName = "forge"
			jarLocation = "build/libs/${rootProject.mod_id}-forge-mc${rootProject.minecraft_version}-${rootProject.mod_version}.jar"
		}

	if (findProject(":neoforge"))
		custom {
			projectName = "neoforge"
			jarLocation = "build/libs/${rootProject.mod_id}-neoforge-mc${rootProject.minecraft_version}-${rootProject.mod_version}.jar"
		}
}

subprojects {
	apply plugin: "dev.architectury.loom"

	loom {
		silentMojangMappingsLicense()
	}

	dependencies {
		minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
		mappings loom.layered {
			it.mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}"
			it.mappings "dev.architectury:yarn-mappings-patch-forge:${rootProject.yarn_mappings_patch}"
		}
	}
}

allprojects {
	apply plugin: "java"
	apply plugin: "architectury-plugin"

	group = rootProject.maven_group
	version = rootProject.mod_version
	archivesBaseName = "${rootProject.mod_id}-mc${rootProject.minecraft_version}"

	repositories {
		maven {
			name = "Jitpack"
			url 'https://jitpack.io/'
		}
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.release = 17
	}

	java {
		withSourcesJar()

		sourceCompatibility = JavaVersion.VERSION_17
		targetCompatibility = JavaVersion.VERSION_17
	}
}
